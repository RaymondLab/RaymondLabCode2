' ------------------------------------------------------------------------------
' INTEGRATOR - CORE SCRIPT USED BY ALL INTEGRATOR PROTOCOLS (REQUIRES SPIKE2 VERSION 10)
'
' Contains variables/functions that are shared by all integrator protocols
' NOTE: Integrator scripts only work with motors in POSITION command mode
' ------------------------------------------------------------------------------
' Define the parent directory all other paths will be relative to
const rootpath$ := "C:/Users/Public/RaymondLabCode2/spike2_experimental_protocols";
FilePathSet(rootpath$, 0);

' These files are the same for ALL EXPERIMENTAL protocols
#include "../utils/read_write_rig_params.s2s"
#include "../utils/sampling_window_config.s2s"
#include "../utils/unit_conversion_functions.s2s"

' Load rig-specific parameters from local-computer's registry
LoadRigParams();

' Gap block parameters
var gapDur := 5.0;  'Total duration (in seconds) of each gap block [default 5.0]

' Testing block parameters
var nTestingBlocks%      := 1;      'Number of pre/post test blocks [default 1]
var testBlockDuration    := 300.0;  'Total duration (s) of test blocks (not including Gap blocks) [default 300.0]

' Training block parameters
var nTrainingBlocks%     := 1;      'Number of training blocks [default 1]
var trainBlockDuration   := 600.0;  'Total duration (s) of training blocks (not including Gap blocks) [default 2700.0]
var testAfterEveryTrain% := 0;      'Determines whether a test block should be performed after each training block [default 1]
var trainingType%        := 0;      'Defines the training type for the experiment [default 0]

' Sinusoidal test stimulus parameters
var sinAmp   := 20.0;  'In deg/s
var sinFreq  := 1.0;   'In Hz
var sinPhase := -90;   'In deg

' Initialize variables for ToolbarIdle% function
var nBlocks%, blockList$[1];
var blockNum%;                                            'Counter which defines which block number is currently active
var pretestBlockNum%, trainBlockNum%, posttestBlockNum%;  'Stores current block numbers by type
var blockMarkers$;                                        'Provides the SampleKey commands for the currently active block
var blockFlag%;                                           'Flag which defines whether a block is actively running
var runFlag%;                                             'Flag which denotes whether the experiment is activately running
var lowerSampleKey$, upperSampleKey$;                     'Stores start/end sampleykeys of current block
var lastMarkerTime;

var blockStartTime, blockEndTime, prevCommandTime, commandVelocity;
var hepos_expression$;
var hepos_vch% := 0;
var scaleCh1 := 1.0; 
var scaleCh2 := 1.0;
var bestChannel% := 0;
var nullCursorNum%;
var nullEyePos := 0.0;
var nullEyePosType% := 0;
var preLastBlockNum% := 2;

' Training block default settings
' Gains correspond to deg/sec of drum velocity per V or deg of eye position
var trainingGains[99];
var trainingSign := -1.0;  '<0 trains to leak, >0 trains to instability
var trainingGains$ := "0.25,0.5,0.75,1.0";
nTrainingBlocks% := ReadStr(trainingGains$, trainingGains[]);
resize trainingGains[nTrainingBlocks%];
var trainingGain := trainingSign * trainingGains[0];

' Null shift default value(s)
var nullShifts[99], nShifts%;
var nullShiftType% := 1;
var nullShifts$    := "5.0";
var nullShift      := Val(nullShifts$);
UpdateNullShifts();

' Null exclusion range default value(s)
var nullExclusions[99], nExclusions%;
var nullExclusionType% := 1;
var nullExclusions$    := "5.0";
var nullExclusion      := Val(nullExclusions$);
UpdateNullExclusions();

' Minumum/maximum allowed velocity command values
var velocityGainMin := 2.0;  'Considered "low" velocity gain in Debowy paper was 2 deg/sec
var velocityGainMax := 40.0;  'Maximum velocity gain used in Major and Debowy papers was 40 deg/sec


'--------------------------------------------------------------------------------
' GENERAL TOOLBAR FUNCTIONS
'--------------------------------------------------------------------------------
' Function that defines what happens during idle times (in computer cycles) while actively sampling
Func ToolbarIdle%()
    ' Check whether a valid sampling view is currently running before continuing
    if ViewKind() <> 0 then
        return 1;
    endif;
    SendDrumCommand();
    if runFlag% = 1 then  'Check whether the "Run Experiment" button was pressed
        blockFlag% := SampleSeqVar(1);  'Get the current blockFlag% value from the 1401 sequencer variable
        if blockFlag% = 0 then  'Check whether a block is not currently running
            if blockNum% > 0 then
                blockEndTime := MaxTime();
                lowerSampleKey$ := Right$(blockMarkers$, 1);
                SampleKey(lowerSampleKey$);  'Sends a lower-case keyboard command that defines the end of a block (useful only for analysis)
                PrintLog("Sample time: %.4f seconds | Block number: %d of %d | Block marker: %s\n", blockEndTime, blockNum%, nBlocks%, lowerSampleKey$);
                docase
                    case (blockNum% <= preLastBlockNum%) and (lowerSampleKey$ = "p") then
                        CalculateNullPosition();
                        CalculateNullExclusionShift();
                        UpdateHeposNull();
                        fileComments$[3] := fileComments$[3] + Print$("(%.3f,%.3f),", blockStartTime, blockEndTime);
                    case lowerSampleKey$ = "t" then
                        if trainBlockNum% < nTrainingBlocks% then
                            CalculateNullExclusionShift();
                        endif;
                        CalculateFracPos();
                        UpdateHeposNull();
                        fileComments$[3] := fileComments$[3] + Print$("(%.3f,%.3f),", blockStartTime, blockEndTime);
                    case (blockNum% > preLastBlockNum%) and (lowerSampleKey$ = "p") then
                        fileComments$[3] := fileComments$[3] + Print$("(%.3f,%.3f),", blockStartTime, blockEndTime);
                endcase;
            endif; 
            if blockNum% < nBlocks% then  'Check whether current block number is not greater than or equal to maximum number of blocks
                blockStartTime := MaxTime();
                blockMarkers$ := blockList$[blockNum%];
                upperSampleKey$ := Left$(blockMarkers$, 1);
                SampleKey(upperSampleKey$);  'Send SampleKey() command to the 1401 sequencer of the current block's letter marker
                AddTextMark(upperSampleKey$, blockNum%);
                blockNum% := blockNum% + 1;  'Increase the block number by one
                PrintLog("Sample time: %.4f seconds | Block number: %d of %d | Block marker: %s\n", blockStartTime, blockNum%, nBlocks%, upperSampleKey$);
            else
                runFlag% := 0;  'Set experiment flag to off
                SampleKey("R");  'Reset 1401 sequencer to initial state
                SampleText("END", -1, 0); 
                SampleKey("a");  'Sends a lower-case keyboard command that defines the end of the experiment (useful only for analysis)
                ToolbarSet(40, "Run Expmt", ToolbarRunExpmt%);
                PrintLog("EXPERIMENT COMPLETE!\n\n");
                Yield(2);
                XRange();  'Zoom out to view entire experiement
            endif;
        endif;
    endif;
    return 1;
End

' Function that stops all stimuli, resets the sequencer to default, and quits out of the toolbar
Func ToolbarQuit%()
    SampleKey("R");
    SampleKey("a");
    SampleStop();
    SetFileComments();
    return 0;
End

' Function that opens a new sampling window
Func ToolbarNewFile%()
    WindowSetup();  'Sets up channels in the new sampling configuration window
    WindowUpdate();  'Modifies window with protocol-specific modifications
    WindowVisible(3);  'Maximizes the new sampling window to fit the screen
    ToolbarEnable(-1, 1);  'Enables buttons that require a new sampling window
    ToolbarEnable(2, 0); 'Disables New File button
    SetSequencerValues(); 
    Yield();
    SampleKey("R");  'Ensures that sequencer is set to its default configuration prior to sampling
    return 1;
End

Func ToolbarFileComments%()
    var ii%;
    DlgCreate("FILE COMMENTS", 0, 0, 85, 7);  'Initialize new dialog window
    DlgString(1, "Comment 1:", 2000, "", 20, 1);
    DlgString(2, "Comment 2:", 2000, "", 20, 2);
    DlgString(3, "Block Labels:", 2000, "", 20, 3);
    DlgString(4, "Start/End Times:", 2000, "", 20, 4);
    DlgString(5, "Null Pos/Excls:", 2000, "", 20, 5);

    DlgAllow(0x3ff, ToolbarIdle%);  'Allow all, no idle
    DlgShow(fileComments$[0], fileComments$[1], fileComments$[2], fileComments$[3], fileComments$[4]);
    
    SetFileComments();
    return 1;
End

' Settings window for modifying general experimental parameters
Func ToolbarExpmtSettings%()
    var width% := 45;
    DlgCreate("EXPERIMENT SETTINGS", 0, 0, width%, 10);  'Initialize new dialog window

    var col1box% := 31;
    DlgText("Number of Pre/Post blocks:", 3, 1);
    DlgInteger(1, 11, 1, 99999, col1box%, 1, 0);
    DlgText("Pre/Post block duration (s):", 3, 2);
    DlgReal(2, 11, 0.001, 99999.0, col1box%, 2, 0);

    DlgText("Number of Training blocks:", 3, 4);
    DlgInteger(3, 11, 1, 99999, col1box%, 4, 0);
    DlgText("Train block duration (s):", 3, 5);
    DlgReal(4, 11, 0.001, 99999.0, col1box%, 5, 0);
    DlgCheck(5, "Perform Test block after each Training block:", 3, 6);

    DlgText("Gap block duration (s):|Total duration of gap blocks", 3, 8);
    DlgReal(6, 11, 0.001, 100.0, col1box%, 8, 0);    

    DlgAllow(0x3ff, ToolbarIdle%);  'Allow all, no idle, update settings function
    DlgShow(nTestingBlocks%, testBlockDuration, nTrainingBlocks%, trainBlockDuration, testAfterEveryTrain%, gapDur);

    ' Re-compute number of cycle steps for GAP1, GAP2, and GAP3 in sequencer file
    CalculateGapSeqSteps();
    CalculateTestSeqSteps();
    CalculateTrainSeqSteps();

    ' Re-generate the block list
    GenerateBlockList(nTestingBlocks%, nTrainingBlocks%);

    ' Send new variable values to the sequencer
    SetSequencerValues();
    return 1;
End

Func ToolbarCalcEyeSign%()
    var tempScale1, tempScale2;
    ' Get the sign (direction) of the VOR stimulus
    var sinAmpSign := sinAmp / Abs(sinAmp);

    ' Run a 1 Hz VOR for some pre-defined duration of time
    SampleKey("S");
    blockStartTime := MaxTime();
    blockFlag% := 1;  'Get the current blockFlag% value from the 1401 sequencer variable
    Yield(0.1);
    while blockFlag% = 1 do
        blockFlag% := SampleSeqVar(1);  'Get the current blockFlag% value from the 1401 sequencer variable
        Yield(0.02);
    wend;
    Yield(1);
    blockEndTime := MaxTime();

    ' Clear any previous fits of eye position channels
    var fitStatus1% := ChanFit(hepos1_ch%, 0, 0);
    var fitStatus2% := ChanFit(hepos2_ch%, 0, 0);
    ' Initialize new sinusoidal fit functions for eye position channels
    fitStatus1% := ChanFit(hepos1_ch%, 4, 1);
    fitStatus2% := ChanFit(hepos2_ch%, 4, 1);
    ' Set initial value and lower/upper limits for frequency (1) and phase (2) coefficients
    var fitholds%[] := {0, 1, 1, 0};
    ChanFitCoef(hepos1_ch%, 1, 1.0*2*_pi, 0.99*2*_pi, 1.01*2*_pi);
    ChanFitCoef(hepos2_ch%, 1, 1.0*2*_pi, 0.99*2*_pi, 1.01*2*_pi);
    ChanFitCoef(hepos1_ch%, 2, 0.0, -0.01*2*_pi, 0.01*2*_pi);
    ChanFitCoef(hepos2_ch%, 2, 0.0, -0.01*2*_pi, 0.01*2*_pi);
    ChanFitCoef(hepos1_ch%, fitholds%[]);
    ChanFitCoef(hepos2_ch%, fitholds%[]);
    ' Perform sinusoidal fits set by previous lines 
    fitStatus1% := ChanFit(hepos1_ch%, 3, blockStartTime, blockEndTime);
    fitStatus2% := ChanFit(hepos2_ch%, 3, blockStartTime, blockEndTime);
    ' Retrieve the zeroth fit coefficient (amplitude) from both fits
    var fitCoef1 := ChanFitCoef(hepos1_ch%, 0);
    var fitCoef2 := ChanFitCoef(hepos2_ch%, 0);

    if sinAmpSign > 0 then
        if fitCoef1 > 0 then
            tempScale1 := -1 * Abs(scaleCh1);
        endif;
        if fitCoef2 > 0 then
            tempScale2 := -1 * Abs(scaleCh2);
        endif;
    else
        if fitCoef1 < 0 then
            tempScale1 := -1 * Abs(scaleCh1);
        endif;
        if fitCoef2 < 0 then
            tempScale2 := -1 * Abs(scaleCh2);
        endif;
    endif;

    ' Display fit results
    var txt$ := Print$("\nhepos1 fit: %.4f | recommended scaleCh1: %.4f", fitCoef1, tempScale1);
    txt$ := txt$ + Print$("\nhepos2 fit: %.4f | recommended scaleCh2: %.4f\n\n", fitCoef2, tempScale2);
    PrintLog(txt$);
    return 1;
End

' Function that runs experiment based on parameters provided in experimental configuration window
Func ToolbarRunExpmt%()
    SampleKey("A");
    SetSequencerValues();
    SampleText("START", -1, 0); 
    ToolbarSet(40, "Stop Expmt", ToolbarStopExpmt%);
    Yield(2);
    runFlag% := 1;
    return 1;
End

' Function that runs experiment based on parameters provided in experimental configuration window
Func ToolbarStopExpmt%()
    SampleKey("R");
    SampleKey("a");
    runFlag% := 0;
    SampleText("END", -1, 0); 
    ToolbarSet(40, "Run Expmt", ToolbarRunExpmt%);
    PrintLog("EXPERIMENT MANUALLY STOPPED!\n\n");
    return 1;
End

'--------------------------------------------------------------------------------
' GENERAL NON-TOOLBAR FUNCTIONS
'--------------------------------------------------------------------------------
Proc CalculateFracPos()
    var npos, ii%, nsamples% := Round((blockEndTime - blockStartTime) * sampleRate);
    var buffer[nsamples%];
    ChanData(hepos_vch%, buffer, blockStartTime, blockEndTime);
    for ii% := 0 to nsamples%-1 do
        if buffer[ii%] > nullEyePos then
            npos := npos + 1;
        endif;
    next;
    var fracPos := npos / (1.0 * nsamples%);
    var blockStd := ChanMeasure(hepos_vch%, 12, blockStartTime, blockEndTime);
    PrintLog("\nFraction of positions above previous null (%.6f deg): %.3f | STD: %.6f\n\n", nullEyePos, fracPos, blockStd);
    docase
        case fracPos >= 0.66 then
            nullEyePos := nullEyePos + nullShift;
        case fracPos <= 0.33 then
            nullEyePos := nullEyePos - nullShift;
    endcase;
End

Proc CalculateNullExclusionShift()
    var heposStd;
    if nullShiftType% = 0 then
        heposStd := ChanMeasure(hepos_vch%, 12, blockStartTime, blockEndTime);
    else
        heposStd := 1.0;
    endif;
    nullShift := nullShifts[trainBlockNum%] * heposStd;
    nullExclusion := nullExclusions[trainBlockNum%] * heposStd;
    PrintLog("\nCaculated Null Exclusion Range: %.6f | Null Shift: %.6f\n\n", nullExclusion, nullShift);
End

Proc CalculateNullPosition()
    var nullEyeDuration := Min(blockEndTime-blockStartTime, 100000);  'Max array size in Spike2
    if nullEyePosType% = 1 then
        'Use quantile mean
        var lowerQ := 0.05;
        var upperQ := 0.95;
        var buffer[nullEyeDuration * 1000];
        var n% := ChanData(hepos_vch%, buffer, blockStartTime, blockEndTime);
        ArrSort(buffer[:n%]);
        var lowerIdx% := Round(n% * lowerQ);
        var upperIdx% := Round(n% * upperQ);
        lowerQ := buffer[lowerIdx%];
        upperQ := buffer[upperIdx%];
        nullEyePos := (lowerQ + upperQ) / 2.0;
    else
        'Use median
        nullEyePos := ChanMeasure(hepos_vch%, 21, blockStartTime, blockEndTime);
    endif;
    PrintLog("\nCaculated Null Position: %.6f deg\n\n", nullEyePos);
End

Proc UpdateHeposNull()
    var bestScaleCh;
    if bestChannel% = 0 then
        hepos_expression$ := Str$(scaleCh1)+"*ch("+Str$(hepos1_ch%)+")";
        bestScaleCh := scaleCh1;
    else
        hepos_expression$ := Str$(scaleCh2)+"*ch("+Str$(hepos2_ch%)+")";
        bestScaleCh := scaleCh2;
    endif;
    VirtualChan(hepos_vch%, hepos_expression$, 0, 1.0/sampleRate);
    if Abs(bestScaleCh) <> 1 then
        ChanUnits$(hepos_vch%, "deg");
    else
        ChanUnits$(hepos_vch%, "V");
    endif;
    HCursor(nullCursorNum%, nullEyePos);
End

Proc UpdateNullShifts()
    var i%, tempShifts[99];
    resize nullShifts[nTrainingBlocks%];
    nShifts% := ReadStr(nullShifts$, tempShifts[]);
    docase
        case nShifts% < nTrainingBlocks% then
            for i% := 0 to nTrainingBlocks%-1 do
                nullShifts[i%] := tempShifts[0];
            next;
        case nShifts% = nTrainingBlocks% then
            for i% := 0 to nTrainingBlocks%-1 do
                nullShifts[i%] := tempShifts[i%];
            next;
    endcase;
    Abs(nullShifts[]);
    nullShifts$ := Print$(nullShifts[]);
End

Proc UpdateNullExclusions()
    var i%, tempExclusions[99];
    resize nullExclusions[nTrainingBlocks%];
    nExclusions% := ReadStr(nullExclusions$, tempExclusions[]);
    docase
        case nExclusions% < nTrainingBlocks% then
            for i% := 0 to nTrainingBlocks%-1 do
                nullExclusions[i%] := tempExclusions[0];
            next;
        case nExclusions% = nTrainingBlocks% then
            for i% := 0 to nTrainingBlocks%-1 do
                nullExclusions[i%] := tempExclusions[i%];
            next;
    endcase;
    Abs(nullExclusions[]);
    nullExclusions$ := Print$(nullExclusions[]);
End